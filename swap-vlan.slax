/*
*
*    NAME: swap-vlan.slax
*    PURPOSE: Created for ? to allow them to change
*             the vlan membership of an Interface
*             from the operations CLI prompt
*
*    CREATED: 09/16/09
*    BY: Jay Wilson (Solutions Architect - Western Area)
*    LAST MOD: 09/16/09
*    BY: Jay Wilson
*    VERSION: 1.00
*
*    MODIFICATION HISTORY:
*        V1.00 = Initial release
*
*/
version 1.0;
ns junos = "http://xml.juniper.net/junos/*/junos";
ns xnm = "http://xml.juniper.net/xnm/1.1/xnm";
ns jcs = "http://xml.juniper.net/junos/commit-scripts/1.0";
import "../import/junos.xsl";

var $arguments = {
    <argument> {
        <name> "interface";
        <description> "Interface that needs to be changed";
    }
    <argument> {
        <name> "vlan";
        <description> "vlan that needs to be on the interface. (options O or T)";
    }
}

param $interface;
param $vlan;

match / {
    if (not($interface)) {
       call write-output($level = "user.error", $id = "ERROR: ", $type = "swap-vlan.slax[Error]: ", $where = "t", $message = "Missing interface param");
    }
    else if (not($vlan == "O") && not($vlan == "T")) {
       call write-output($level = "user.error", $id = "ERROR: ", $type = "swap-vlan.slax[Error]: ", $where = "t", $message = "vlan param must be O or T");
    }
    else {
        var $r-vlan = { if ($vlan == "O" ) {
                           expr "test";
                        }
                        else {
                            expr "operations";
                        }
                      }
        var $a-vlan = { if ($vlan == "O" ) {
                           expr "operations";
                        }
                        else {
                            expr "test";
                        }
                      }
/*
*
*   Open a connection with mgd
*
*/
        var $con = jcs:open();
        if (not($con)) {
            call write-output($level = "user.error", $id = "ERROR: ", $type = "swap-vlan.slax[Error]: ", $where = "b", $message = "Not able to connect to local mgd");
        }
/*
*
*   XML structure to change the VLAN on the INTERFACE
*
*/
        var $xml = {
            <configuration> {
                 <interfaces> {
                     <interface> {
                         <name> $interface;
                         <unit> {
                             <name> "0";
                             <family> {
                                 <ethernet-switching> {
                                     <vlan> {
                                         <members delete="delete"> $r-vlan;
                                         <members> $a-vlan;
                                     } 
                                 }
                             }
                         }
                     }
                 }
            }
        }

/*
*
*   Use load-configuration template defined in junos.xsl to load and
*   commit the configuration
*
*/
        var $results := {
            call jcs:load-configuration($connection = $con, $configuration = $xml);
        }

/*
*
*   Check the results and process them
*
*/

        for-each ($results//xnm:warning) {
            if (not(contains(message,"statement not found:"))) {
            call write-output($level = "user.warning", $id = "WARNING: ", $type = "swap-vlan.slax[Warning]: ", $where = "b", $message = message);
            }
        }
        if ($results//xnm:error) {
            for-each ($results//xnm:error) {
                call write-output($level = "user.error", $id = "ERROR: ", $type = "swap-vlan.slax[Error]: ", $where = "b", $message = message);
            }
        }
        else {
            var $message = "Successfully swapped vlan " _ $a-vlan _ " for " _ $r-vlan _ " on interface " _ $interface;
            call write-output($level = "user.info", $id = "INFO: ", $type = "swap-vlan.slax[Success]: ", $where = "b", $message = $message);
        }

/*
*
*   Close the mgd connection
*
*/
        expr jcs:close($con);
    }
}

/*
*   NAME: WRITE-OUTPUT
*   PURPOSE: Writes a message to standard and/or the syslog depending on 
*            the parms passed to it.
*   CALLED: Called after the XML changes have been submitted to MGD process.
*
*
*   PARMS PASSED:
*        $level = The syslog message level
*        $id = Standout message ID
*        $type = The string identifying the type of message
*        $message = The string to print out 
*        $where = Used to specify if "b"oth STDOUT and SYSLOG
*                 should be written to or only "S"TDOUT
*
*/
template write-output($level = "user.info", $id = "INFO: ", $type = " ", $where = "b", $message) {
     var $stdmessage = $id _ $message;

     if ($where == "s") {
        expr jcs:output($stdmessage);
     }
     else {
         expr jcs:syslog($level, $type, $message);
         
         expr jcs:output($stdmessage);
     }         

}

